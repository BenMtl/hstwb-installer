; Install Complete
; ----------------
;
; Date: 2017-09-05
; Author: Henrik Noerfjand Stengaard
;
; An AmigaDOS script to complete installation by rebooting system or quiting UAE.


; Create install complete prefs
echo "" >INSTALLDIR:Prefs/Install-Complete


; Clear screen
cls


; Print self install or install complete message
IF EXISTS INSTALLDIR:Prefs/Self-Install
  echo "Self Install complete"
  echo "---------------------"
ELSE
  echo "Install complete"
  echo "----------------"
ENDIF
echo ""


IF EXISTS INSTALLDIR:Prefs/Install-BoingBags
  IF EXISTS INSTALLDIR:Prefs/UAE
    echo "HstWB Installer has completed installation and will"
    echo "quit and restart UAE to start installation of"
    echo "Boing Bags in Amiga OS 3.9 during WBStartup."
  ELSE
    echo "HstWB Installer has completed installation and will"
    echo "reboot system to start installation of"
    echo "Boing Bags in Amiga OS 3.9 during WBStartup."
  ENDIF
ELSE
  echo "HstWB Installer has completed installation and system is"
  echo "now ready for use."
ENDIF
echo ""


; Set uae to 1, if use prefs exists
set uae "0"
IF EXISTS INSTALLDIR:Prefs/UAE
  set uae "1"
ENDIF


; Run install cleanup, if self install exists prefs, install boing bags and install packages prefs doesn't exist
IF EXISTS INSTALLDIR:Prefs/Self-Install
  IF NOT EXISTS INSTALLDIR:Prefs/Install-BoingBags
    IF NOT EXISTS INSTALLDIR:Prefs/Install-Packages
      execute INSTALLDIR:S/Install-Cleanup
    ENDIF
  ENDIF
ENDIF


echo "Please let the system wait 10 seconds before continuing"
echo "to allow file system to write changes to disk." 
echo ""

; Quit UAE or reboot message
IF $uae EQ 1 VAL
  echo "Continue will quit UAE in 10 seconds."
ELSE
  echo "Continue will reboot system in 10 seconds."
ENDIF

echo ""
ask "Press ENTER to continue"
echo ""

; Quit UAE, if UAE is used. Otherwise reboot
IF $uae EQ 1 VAL
  echo "UAE will quit in 10 seconds..."
  wait 10
  UAEquit
ELSE
  echo "System will reboot in 10 seconds..."
  wait 10
  Reboot
ENDIF