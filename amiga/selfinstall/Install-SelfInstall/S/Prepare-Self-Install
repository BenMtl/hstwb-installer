; Prepare Self Install
; --------------------
;
; Date: 2017-08-03
; Author: Henrik Noerfjand Stengaard
;
; An AmigaDOS script to prepare self install by detecting precense of CD drive, Amiga OS 3.9 iso and Boing Bags to configure installation flow.


; Clear screen
echo "*ec" 


; Delete install workbench prefs, if it exists
IF EXISTS "INSTALLDIR:Prefs/Install-Workbench"
  Delete >NIL: "INSTALLDIR:Prefs/Install-Workbench"
ENDIF


; Delete install amiga os 3.9 prefs, if it exists
IF EXISTS "INSTALLDIR:Prefs/Install-AmigaOS3.9"
  Delete >NIL: "INSTALLDIR:Prefs/Install-AmigaOS3.9"
ENDIF


; Delete install kickstart prefs, if it exists
IF EXISTS "INSTALLDIR:Prefs/Install-Kickstart"
  Delete >NIL: "INSTALLDIR:Prefs/Install-Kickstart"
ENDIF


; Delete install boing bags prefs, if it exists
IF EXISTS "INSTALLDIR:Prefs/Install-BoingBags"
  Delete >NIL: "INSTALLDIR:Prefs/Install-BoingBags"
ENDIF


echo "Device Names Overview"
echo "---------------------"
echo ""
echo "Using UAE emulator HstWB-Installer can detect and use"
echo "following device names for installation of Amiga OS 3.9,"
echo "Workbench 3.1 and Kickstart roms:"
echo "- CD0: CD drive with Amiga OS 3.9 CD-Rom."
echo "- OS39DIR: Directory with Amiga 3.9 iso and boing bag files."
echo "  Note: Directory must be read/write, if installing Amiga"
echo "  OS 3.9 from iso (required by filedisk.device)."
echo "- WORKBENCHDIR: Directory with Workbench 3.1 adf files."
echo "- KICKSTARTDIR: Directory with Kickstart rom files."
echo ""
ask "Press ENTER to continue"


; Clear screen
echo "*ec" 


echo "Device Names Status"
echo "-------------------"
echo ""
echo "HstWB-Installer has detected device names with"
echo "following status:"


; Set installso39 to 1
set installos39 "1"


; Set fail at 255 for DiskInDrive to fail silently
failat 255


; Set installos39 to 0, if device CD0: doesn't contain a cd-rom
DiskInDrive >NIL: CD0:
IF WARN
  set installos39 "0"
ENDIF


; Set fail at 21
failat 21


; Check if CD0: and AmigaOS3.9: device names exists and set installos39
IF $installos39 EQ 1 VAL
  Assign >NIL: EXISTS AmigaOS3.9:
  IF WARN
    set installos39 "0"
  ENDIF
ENDIF


; Print cd drive status depending on installos39
IF $installos39 EQ 1 VAL
  echo "- CD0: Present! Install Amiga OS 3.9 from CD drive."
ELSE
  echo "- CD0: Not present! Skip Amiga OS 3.9 installation"
  echo "  from CD drive."
ENDIF


; Set os39dir to 1, if device name OS39DIR: exists
set os39dir "0"
Assign >NIL: EXISTS OS39DIR:
IF NOT WARN
  ; Set fail at 255 for DiskInDrive to fail silently
  failat 255

  ; Set os39dir to 1, if device OS39DIR: is mapped to a directory
  DiskInDrive >NIL: OS39DIR:
  IF NOT WARN
    set os39dir "1"
  ENDIF

  ; Set fail at 21
  failat 21
ENDIF


; Check for amiga os 3.9 iso and boing bags, if OS39DIR: device name exists
set amigaos39isofile ""
IF $os39dir EQ 1 VAL
  ; Find boing bag files
  fsearch "OS39DIR:" PAT BoingBag39#?.lha >T:_boingbagfiles1
  frsort <T:_boingbagfiles1 >T:_boingbagfiles

  IF $installos39 EQ 0 VAL
    ; Find amiga os 3.9 iso files
    echo "" NOLINE >T:_amigaos39isofiles
    fsearch "OS39DIR:" PAT amigaos39.iso >>T:_amigaos39isofiles
    fsearch "OS39DIR:" PAT amigaos3.9.iso >>T:_amigaos39isofiles

    ; Get first amiga os 3.9 iso file
    sed "1q;d" T:_amigaos39isofiles >T:_amigaos39isofile
    set amigaos39isofile "`type T:_amigaos39isofile`"
    IF NOT "$amigaos39isofile" EQ ""
      set installos39 "1"
      echo "- OS39DIR: Present! Install Amiga OS 3.9 from iso and"
      echo "  apply Boing Bag updates."
    ELSE
      echo "- OS39DIR: Present! Apply Boing Bag updates."
    ENDIF
  ELSE
    echo "- OS39DIR: Present! Apply Boing Bag updates."
  ENDIF
ELSE
  echo "- OS39DIR: Not present! Skip Amiga OS 3.9 installation"
  echo "  from iso and Boing Bag updates."
ENDIF


; Skip Workbench 3.1 installation, if Amiga OS 3.9 will be installed
IF $installos39 EQ 1 VAL
  echo "- WORKBENCHDIR: Ignored! Skip Workbench 3.1 installation,"
  echo "  when Amiga OS 3.9 will be installed."
ELSE
  ; Check if WORKBENCHDIR: and set workbench
  set workbench "0"
  Assign >NIL: EXISTS WORKBENCHDIR:
  IF NOT WARN
    ; Set fail at 255 for DiskInDrive to fail silently
    failat 255

    ; Set workbench to 1, if device WORKBENCHDIR: is mapped to a directory
    DiskInDrive >NIL: WORKBENCHDIR:
    IF NOT WARN
      set workbench "1"
    ENDIF

    ; Set fail at 21
    failat 21
  ENDIF

  ; Print workbench status and create install workbench prefs, if WORKBENCHDIR: exists
  IF $workbench EQ 1 VAL
    ; Create install workbench prefs
    echo "" >INSTALLDIR:Prefs/Install-Workbench

    echo "- WORKBENCHDIR: Present! Install Workbench 3.1 from adf."
  ELSE
    echo "- WORKBENCHDIR: Not present! Skip Workbench 3.1 installation"
    echo "  from adf and install Workbench 3.1 from disks."
  ENDIF  
ENDIF


; Check if KICKSTARTDIR: and set kickstart
set kickstart "0"
Assign >NIL: EXISTS KICKSTARTDIR:
IF NOT WARN
  ; Set fail at 255 for DiskInDrive to fail silently
  failat 255

  ; Set workbench to 1, if device KICKSTARTDIR: is mapped to a directory
  DiskInDrive >NIL: KICKSTARTDIR:
  IF NOT WARN
    set kickstart "1"
  ENDIF

  ; Set fail at 21
  failat 21
ENDIF


; Print kickstart status and create install kickstart prefs, if KICKSTARTDIR: exists
IF $kickstart EQ 1 VAL
  ; Create install kickstart prefs
  echo "" >INSTALLDIR:Prefs/Install-Kickstart

  echo "- KICKSTARTDIR: Present! Install Kickstart rom files."
ELSE
  echo "- KICKSTARTDIR: Not present! Skip Kickstart rom installation."
ENDIF


; Create backup of mountlist, if it doesn't exist. Otherwise overwrite mountlist with backup.
IF NOT EXISTS INSTALLDIR:Devs/Mountlist.bak
  Copy >NIL: INSTALLDIR:Devs/Mountlist INSTALLDIR:Devs/Mountlist.bak
ELSE
  Copy >NIL: INSTALLDIR:Devs/Mountlist.bak INSTALLDIR:Devs/Mountlist
ENDIF


; Replace os39isofilename placeholder in mountlist 
IF NOT "$amigaos39isofile" EQ ""
  rep INSTALLDIR:Devs/Mountlist "OS39DIR:[*$OS39IsoFileName]" "$amigaos39isofile"
ELSE
  rep INSTALLDIR:Devs/Mountlist "OS39DIR:[*$OS39IsoFileName]" ""
ENDIF


; Install amiga os 3.9, if installos39 is 1
IF $installos39 EQ 1 VAL 
  ; Create install amiga os 3.9 prefs, if installos39 is 1
  echo "" >INSTALLDIR:Prefs/Install-AmigaOS3.9

  ; Print amiga os 3.9 iso file, if present
  IF NOT "$amigaos39isofile" EQ ""
    echo ""
    echo "Using Amiga OS 3.9 iso file:"
    echo "$amigaos39isofile"
  ENDIF

  ; Examine if boing bag files exists
  IF EXISTS T:_boingbagfiles
    set boingbagfilescount `wc -l <T:_boingbagfiles`

    ; Install boing bags, if one or more boing bag files exists
    echo ""
    IF $boingbagfilescount gt 0 VAL
      ; Create install boing bags prefs
      echo "" >INSTALLDIR:Prefs/Install-BoingBags

      ; Print boing bag files
      echo "Using Boing Bag file(s):"
      type T:_boingbagfiles
    ELSE
      echo "Boing Bag file(s) doesn't exist. Skip Boing Bags"
      echo "installation."
    ENDIF
  ENDIF
ENDIF


echo ""
ask "Press ENTER to continue"