; Startup Sequence
; ----------------
;
; Date: 2019-01-07
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script for HstWB Installer self install


; set failat to 21
FailAt 21

; load patched scsi.device v43.45, if it exists
IF EXISTS DEVS:scsi.device
  ; Load scsi.device, if current loaded scsi.device is less than v43.45
  Version >NIL: "scsi.device" 43 45
  IF WARN
    LoadModule DEVS:scsi.device
  ENDIF
ENDIF

; add alias
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" " 

; set stack
stack 16384

; add assigns from S:AssignList using assplus
MakePath >NIL: RAM:Env
AssPlus >NIL:

; mount kingcon
Mount KCON: from INSTALLDIR:Devs/Mountlist-KingCON
Mount KRAW: from INSTALLDIR:Devs/Mountlist-KingCON 

; run start-selfinstall in new kingcon shell
NewShell WINDOW "KCON:0/0/640/300/HstWB Installer v[$VersionText]/AUTO" FROM "INSTALLDIR:S/Start-SelfInstall"
EndShell
