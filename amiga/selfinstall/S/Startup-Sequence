; Startup Sequence for HstWB Installer
; ------------------------------------
;
; Date: 2017-10-27
; Author: Henrik Noerfjand Stengaard


; Load patched scsi.device v43.45
LoadModule DEVS:scsi.device


; Add alias
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" " 


; Startup message
cls 
echo "*e[32m" NOLINE
echo "HstWB Installer"
echo "*e[0m*e[1m" NOLINE
echo "---------------"
echo "*e[0m" NOLINE
echo ""
echo "HstWB Installer is now running build self install."


; Set stack, failat and load basic commands resident 
stack 16384
failat 21
DF0:C/AddBuffers >NIL: DF0: 200
resident DF0:C/Assign PURE
resident DF0:C/Copy PURE
resident DF0:C/Delete PURE
resident DF0:C/Execute PURE
resident DF0:C/MakeDir PURE
resident DF0:C/Protect PURE
resident DF0:C/Search PURE 
resident DF0:C/Wait PURE 
MakeDir RAM:T RAM:Clipboards RAM:ENV RAM:ENV/Sys
Assign ENV: RAM:Env
SetEnv TZ MST7


; Fail, if assign hstwb installer doesn't exist
IF NOT EXISTS S:Assign-HstWB-Installer
  echo "" >INSTALLDIR:Error
  echo "Error: assign hstwb installer doesn't exist!"
  SKIP end
ENDIF
execute S:Assign-HstWB-Installer


; Load commands resident 
resident INSTALLDIR:C/Reboot PURE
resident INSTALLDIR:C/TotalReset PURE
resident INSTALLDIR:C/UnZip PURE 
resident INSTALLDIR:C/UAEquit PURE 
resident INSTALLDIR:C/MakePath PURE 


echo ""
echo "*e[1mBuilding self install*e[0m"
echo "Copying boot scripts and tools..."
copy >NIL: "INSTALLDIR:Boot-SelfInstall/" TO "SYSTEMDIR:" ALL
IF NOT EXISTS "SYSTEMDIR:Prefs/Env-Archive"
  MakePath >NIL: "SYSTEMDIR:Prefs/Env-Archive"
ENDIF
copy >NIL: "INSTALLDIR:Prefs/Env-Archive" "SYSTEMDIR:Prefs/Env-Archive" ALL


; Create install directory, if it doesn't exist
IF NOT EXISTS "HSTWBINSTALLERDIR:Install/"
  MakePath >NIL: "HSTWBINSTALLERDIR:Install"
ENDIF


; Copy self install scripts and tools
echo "Copying self install scripts and tools..."
copy >NIL: "INSTALLDIR:Install-SelfInstall" TO "HSTWBINSTALLERDIR:Install/" ALL


; Create packages directory, if it doesn't exist
IF NOT EXISTS "HSTWBINSTALLERDIR:Packages/"
  MakePath >NIL: "HSTWBINSTALLERDIR:Packages"
ENDIF


; Copy Packages
echo "Copying packages..."
copy >NIL: "PACKAGESDIR:" TO "HSTWBINSTALLERDIR:Packages/" ALL


; Create licenses directory, if it doesn't exist
IF NOT EXISTS "HSTWBINSTALLERDIR:Licenses/"
  MakePath >NIL: "HSTWBINSTALLERDIR:Licenses"
ENDIF

; Copy licenses
echo "Copying licenses..."
copy >NIL: "INSTALLDIR:Licenses" TO "HSTWBINSTALLERDIR:Licenses/" ALL


; Create install complete prefs
echo "" >INSTALLDIR:Prefs/Install-Complete


; Done
echo "Done"
echo ""
echo "Installation is now complete."
echo ""
echo "Please let the system wait 10 seconds before continuing"
echo "to allow file system to write changes to disk." 
echo ""
echo "Continue will quit UAE in 10 seconds."
echo ""
ask "Press ENTER to continue"
echo ""


; End
LAB end
echo "UAE will quit in 10 seconds..."
wait 10
uaequit >NIL: