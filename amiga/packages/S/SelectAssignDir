; Select assign dir
; -----------------
LAB selectassigndir

; Set assign dir to SYS:, if not defined
IF "$assigndir" eq ""
  set assigndir "SYS:"
ELSE
  echo "$assigndir" >T:_assigndir
  rep T:_assigndir ":" "*N"
  set devicename "`sed "1q;d" T:_assigndir`"
  delete >NIL: "T:_assigndir"

  Assign >NIL: EXISTS "$devicename:"
  IF WARN
    set assigndir "SYS:"
  ENDIF
ENDIF

; Set assign dir to SYS:, if assign dir doesn't exist
IF NOT EXISTS "$assigndir"
  set assigndir "SYS:"
ENDIF

; Show select dir for assign dialog"
set newassigndir ""
set newassigndir `REQUESTFILE DRAWER "$assigndir" TITLE "Select '$assignname' assign" NOICONS DRAWERSONLY`

; Return, if select dir for assign dialog is cancelled"
IF "$newassigndir" eq ""
  SKIP $returnlab
ENDIF

; Write new assign dir for assign id
echo "$newassigndir" >"T:$assignid"

; Strip tailing slash from assign dir
sed "s/\/$//" "T:$assignid" >"T:_assigndir"
copy >NIL: "T:_assigndir" "T:$assignid"
delete >NIL: "T:_assigndir"

SKIP $returnlab