; Find Workbench Adf
; ------------------
;
; Date: 2017-08-14
; Author: Henrik Noerfjand Stengaard


; Assign FMS to install temp dir, if self install prefs exists. Otherwise assign FMS to T:
IF EXISTS INSTALLDIR:Prefs/Self-Install
  ; Create fms temp directory, if it doesn't exist
  IF NOT EXISTS "INSTALLDIR:Temp/FMS"
    makepath >NIL: "INSTALLDIR:Temp/FMS"
  ENDIF

  ; Assign FMS: to fms temp directory, if it doesn't exist
  Assign >NIL: EXISTS FMS:
  IF WARN
    assign >NIL: FMS: INSTALLDIR:Temp/FMS
  ENDIF
ELSE
  ; Assign FMS: to T:, if it doesn't exist
  Assign >NIL: EXISTS FMS:
  IF WARN
    assign >NIL: FMS: T:
  ENDIF
ENDIF


; Mount FF0:, if it doesn't exist
Assign >NIL: EXISTS FF0:
IF WARN
  Mount FF0: FROM INSTALLDIR:Devs/Mountlist
ENDIF


; Find Workbench adf files
echo ""
echo "Finding Workbench adf files in WORKBENCHDIR:"
fsearch WORKBENCHDIR:#?.adf >T:workbench-adf-files1
sed "s/^\(.\)/execute *"INSTALLDIR:S\/Copy-Workbench-Adf*" *"\1/" T:workbench-adf-files1 >T:workbench-adf-files2
sed "s/\(.\)$/\1*" *"INSTALLDIR:Workbench\/*"/" T:workbench-adf-files2 >T:workbench-adf-files3
execute T:workbench-adf-files3
echo "Done"