; Install Boing Bags Script
; -------------------------
;
; Date: 2017-07-07
; Author: Henrik Noerfjand Stengaard
;
; An AmigaDOS script to automate installation of Boing Bag 1.


; Install Boing Bag 1
; -------------------

echo ""
echo "Boing Bag 1 installation"


; Extract boing bag 1
echo "Extracting..."
INSTALLDIR:C/lha -m1 x "OS39DIR:BoingBag39-1.lha" SYS:T/BoingBags/ >NIL:
echo "Installing..."


; Run boing bag 1 updater
SYS:T/BoingBags/BoingBag3.9-1/C/Updater SYS:T/BoingBags/BoingBag3.9-1/AmigaOS-Update "SYS:"


; Set some protection bits 
protect SYS:C/LoadMonDrvs +prwed
protect SYS:C/LoadResource +prwed
protect SYS:C/MakeDir +prwed
protect SYS:C/MakeLink +prwed
protect SYS:C/SetEnv +prwed
protect SYS:C/WBInfo +prwed
protect SYS:C/WBRun +prwed

protect SYS:S/Start-Amplifier.rexx +srwed
protect SYS:S/Startup-Sequence-BB3.9-1 +srwed
protect SYS:S/Stream-Amplifier.rexx +srwed


; Install WarpUP 5.1
IF EXISTS "SYS:T/BoingBags/BoingBag3.9-1/PowerPC/powerpc.library"
  IF EXISTS "SYS:Libs/powerpc.library"
    Copy >NIL: "SYS:T/BoingBags/BoingBag3.9-1/PowerPC/powerpc.library" "SYS:Libs"
  ENDIF
ENDIF

IF EXISTS "SYS:T/BoingBags/BoingBag3.9-1/PowerPC/warp.library"
  IF EXISTS "SYS:Libs/warp.library"
    Copy >NIL: "SYS:T/BoingBags/BoingBag3.9-1/PowerPC/warp.library" "SYS:Libs"
  ENDIF
ENDIF


; Install locale catalogs
execute S:Copy-Locale "SYS:T/BoingBags/BoingBag3.9-1/Locale/Catalogs" "SYS:Locale/Catalogs"

IF EXISTS "SYS:Internet/AWeb3SE/Catalogs"
  execute S:Copy-Locale "SYS:T/BoingBags/BoingBag3.9-1/Locale/AWeb3SE/Catalogs" "SYS:Internet/AWeb3SE/Catalogs"
ENDIF


; Rename internet directory and files
IF EXISTS "SYS:Internet"
  IF EXISTS "SYS:Internet/GenesisRA"
    IF EXISTS "SYS:Internet/GenesisRA/Genesis"
      Rename >NIL: "SYS:Internet/GenesisRA/Genesis" "SYS:Internet/GenesisRA/GenesisMUI"
      Rename >NIL: "SYS:Internet/GenesisRA/Genesis.info" "SYS:Internet/GenesisRA/GenesisMUI.info"
    ENDIF

    Rename >NIL: "SYS:Internet/GenesisRA" "SYS:Internet/Genesis"
    Rename >NIL: "SYS:Internet/GenesisRA.info" "SYS:Internet/Genesis.info"
  ENDIF

  IF EXISTS "SYS:Internet/GenesisWizardRA"
    IF EXISTS "SYS:Internet/GenesisWizardRA/GenesisWizard"
      Rename >NIL: "SYS:Internet/GenesisWizardRA/GenesisWizard" "SYS:Internet/GenesisWizardRA/GenesisWizardMUI"
      Rename >NIL: "SYS:Internet/GenesisWizardRA/GenesisWizard.info" "SYS:Internet/GenesisWizardRA/GenesisWizardMUI.info"
    ENDIF

    Rename >NIL: "SYS:Internet/GenesisWizardRA" "SYS:Internet/GenesisWizard"
    Rename >NIL: "SYS:Internet/GenesisWizardRA.info" "SYS:Internet/GenesisWizard.info"
  ENDIF

  Copy >NIL: "SYS:T/BoingBags/BoingBag3.9-1/Internet" "SYS:Internet" ALL
ENDIF

echo "Done"


; End
; ---
LAB end