; Install Boing Bags Script
; -------------------------
;
; Date: 2018-12-23
; Author: Henrik Noerfjand Stengaard 
;
; An AmigaDOS script to automate installation of Amiga OS 3.9 Boing Bags.


; add cls alias
Alias cls "Echo NOLINE *"*E[0;0H*E[J*" " 

; amiga os 3.9 boing bags installation message
cls
echo "*e[32m" NOLINE
echo "Amiga OS 3.9 Boing Bags installation"
echo "*e[0m*e[1m" NOLINE
echo "------------------------------------"
echo "*e[0m" NOLINE
echo ""

; goto end, if device AMIGAOSDIR: doesn't exist
Assign >NIL: EXISTS AMIGAOSDIR:
IF WARN
  echo "*e[1mERROR: Device AMIGAOSDIR: doesn't exist!*e[22m"
  SKIP end
ENDIF

; goto end, if boing bag 1 file doesn't exist
IF NOT EXISTS "AMIGAOSDIR:BoingBag39-1.lha"
  echo ""
  echo "*e[1mERROR: Boing bags 1 file 'BoingBag39-1.lha' doesn't exist in"
  echo "device AMIGAOSDIR:. Skipping Amiga OS 3.9 Boing Bags installation!*e[22m"
  SKIP end
ENDIF

; goto end, if version.library is not version 45 (Amiga OS 3.9)
Version >NIL: "SYS:Libs/version.library" 45 FILE
IF WARN
  echo "*e[1mERROR: Boing Bags installation requires AmigaOS 3.9!*e[22m"
  SKIP end
ENDIF

; goto end, if assign hstwb installer doesn't exist
IF NOT EXISTS S:Assign-HstWB-Installer
  echo "*e[1mERROR: Assign hstwb installer doesn't exist!*e[22m"
  SKIP end
ENDIF

; run assign hstwb installer
execute S:Assign-HstWB-Installer

; goto end, if device INSTALLDIR: doesn't exist
Assign >NIL: EXISTS INSTALLDIR:
IF WARN
  echo "*e[1mERROR: Device INSTALLDIR: doesn't exist!*e[22m"
  SKIP end
ENDIF

; add assigns to installdir
Assign >NIL: C: INSTALLDIR:C ADD
Assign >NIL: Devs: INSTALLDIR:Devs ADD
Assign >NIL: L: INSTALLDIR:L ADD
Assign >NIL: Libs: INSTALLDIR:Libs ADD
Assign >NIL: Prefs: INSTALLDIR:Prefs ADD

; load commands resident 
Resident >NIL: "INSTALLDIR:C/MakePath" PURE 
Resident >NIL: "INSTALLDIR:C/Reboot" PURE 
Resident >NIL: "INSTALLDIR:C/TotalReset" PURE
Resident >NIL: "INSTALLDIR:C/UAEquit" PURE
Resident >NIL: "INSTALLDIR:C/LhA" PURE
Resident >NIL: "INSTALLDIR:C/UNLZX" PURE
Resident >NIL: "INSTALLDIR:C/UnZip" PURE


echo ""
echo "Mounting Amiga OS 3.9 iso..."
Mount FCD0: FROM INSTALLDIR:Devs/Mountlist
wait 5
echo "Done"



; goto end, if device AmigaOS3.9: doesn't exist
Assign >NIL: EXISTS AmigaOS3.9:
IF WARN
  echo ""
  echo "*e[1mERROR: Device AmigaOS3.9: doesn't exist!*e[22m"
  SKIP end
ENDIF


; boing bag 1
; -----------

; create temp boing bags directory
makedir >NIL: SYS:T/BoingBags

; run install boing bag 1
execute S:Install-BoingBag1

; goto complete, if boing bag 2 file doesn't exist
IF NOT EXISTS "AMIGAOSDIR:BoingBag39-2.lha"
  SKIP complete
ENDIF


; boing bag 2
; -----------

; run install boing bag 2
execute S:Install-BoingBag2


; complete
; --------
LAB complete

; remove assigns from installdir
Assign >NIL: C: INSTALLDIR:C REMOVE
Assign >NIL: Devs: INSTALLDIR:Devs REMOVE
Assign >NIL: L: INSTALLDIR:L REMOVE
Assign >NIL: Libs: INSTALLDIR:Libs REMOVE
Assign >NIL: Prefs: INSTALLDIR:Prefs REMOVE

; delete temp boing bags directory
Delete >NIL: SYS:T/BoingBags ALL

; delete install boing bags scripts from systemdir
Delete >NIL: SYS:S/Install-BoingBags
Delete >NIL: SYS:WBStartup/Start-Install-BoingBags#?

; replace workbench startup-sequence with packages startup-sequence, if install packages prefs exists
IF EXISTS INSTALLDIR:System/Prefs/Install-Packages
  Rename >NIL: SYSTEMDIR:S/Startup-Sequence SYSTEMDIR:S/Startup-Sequence.Workbench
  Copy >NIL: INSTALLDIR:S/Startup-Sequence.Packages SYSTEMDIR:S/Startup-Sequence
ENDIF

echo ""
echo "Boing Bags installation is complete."
echo ""


; reboot message
IF EXISTS INSTALLDIR:System/Prefs/Install-Packages
  echo "HstWB Installer has completed installation of"
  echo "Amiga OS 3.9 Boing Bags and will now reboot to"
  echo "start package installation."
ELSE
  echo "HstWB Installer has completed installation of"
  echo "Amiga OS 3.9 Boing Bags and system is now"
  echo "ready for use."
ENDIF
echo ""


; set uae to 1, if use prefs exists
set uae "0"
IF EXISTS INSTALLDIR:Prefs/UAE
  set uae "1"
ENDIF


; set installpackages to 1, if install packages prefs exists
set installpackages "0"
IF EXISTS INSTALLDIR:System/Prefs/Install-Packages
  set installpackages "1"
ENDIF

; set installpackages to 1, if install user packages prefs exists
IF EXISTS INSTALLDIR:Prefs/Install-User-Packages
  set installpackages "1"
ENDIF

; run install cleanup, if install packages is 0
IF "$installpackages" EQ 0 VAL
  execute INSTALLDIR:S/Install-Cleanup
ENDIF


echo "Please let the system wait 10 seconds before continuing"
echo "to allow file system to write changes to disk." 


; Reboot system, if install packages prefs exist
IF $installpackages EQ 1 VAL
  echo ""
  ask "Press ENTER to continue"
  echo ""
  echo "System will reboot in 10 seconds..."
  wait 10
  TotalReset >NIL:
ENDIF


; End
; ---
LAB end

echo ""
ask "Press ENTER to continue"
echo ""


; Quit uae, if uae is used. Otherwise reboot
IF $uae EQ 1 VAL
  echo "UAE will quit in 10 seconds..."
  wait 10
  UAEquit >NIL:
ELSE
  echo "System will reboot in 10 seconds..."
  wait 10
  TotalReset >NIL:
ENDIF

ENDSHELL