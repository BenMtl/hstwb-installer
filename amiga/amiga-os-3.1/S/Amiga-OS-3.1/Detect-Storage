; Detect Amiga OS 3.1 Storage
; ---------------------------
;
; Date: 2018-12-07
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to detect valid amiga os 3.1 storage floppy disk or adf file.


set diskvalid "0"

; end, if amiga os disk  volume name is not "Storage3.1:"
IF NOT "`Which $amigaosdisk:`" EQ "Storage3.1:"
  SKIP end
ENDIF

; end, if rad doesn't exist
IF NOT EXISTS "$amigaosdisk:DosDrivers/RAD"
  SKIP end
ENDIF

; end, if dosdrivers aux doesn't exist
IF NOT EXISTS "$amigaosdisk:DOSDrivers/AUX"
  SKIP end
ENDIF

; end, if dosdrivers aux is greater than v40.1
Version >NIL: "$amigaosdisk:DOSDrivers/AUX" 40 2 FILE
IF $RC EQ 0 VAL
  SKIP end
ENDIF

; end, if dosdrivers aux is less than v40.1
Version >NIL: "$amigaosdisk:DOSDrivers/AUX" 40 0 FILE
IF NOT $RC EQ 0 VAL
  SKIP end
ENDIF

set diskvalid "1"


; end
; ---

LAB end
