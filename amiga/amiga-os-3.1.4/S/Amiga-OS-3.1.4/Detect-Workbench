; Detect Amiga OS 3.1.4 Workbench
; -------------------------------
;
; Date: 2018-10-14
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to detect valid amiga os 3.1.4 workbench floppy disk or adf file.

set workbenchvalid "0"

; end, if which doesn't exist
IF NOT EXISTS "$amigaosdisk:C/Which"
  SKIP end
ENDIF

; end, if assign doesn't exist
IF NOT EXISTS "$amigaosdisk:C/Assign"
  SKIP end
ENDIF

; end, if datatypes.library doesn't exist
IF NOT EXISTS "$amigaosdisk:Libs/datatypes.library"
  SKIP end
ENDIF

; end, if datatypes.library is less than v46.3
Version >NIL: "$amigaosdisk:Libs/datatypes.library" 46 2 FILE
IF NOT $RC EQ 0 VAL
  SKIP end
ENDIF

; end, if cli doesn't exist
IF NOT EXISTS "$amigaosdisk:System/CLI"
  SKIP end
ENDIF

; end, if shell.info doesn't exist
IF NOT EXISTS "$amigaosdisk:System/Shell.info"
  SKIP end
ENDIF

IF "$amigaosfloppy" EQ 1 VAL
  IF NOT "`$amigaosdisk:c/which $amigaosdisk:`" EQ "Workbench3.1.4:"
    SKIP end
  ENDIF
ENDIF

; end, if version.library doesn't exist
IF NOT EXISTS "$amigaosdisk:Libs/version.library"
  SKIP end
ENDIF

; end, if version.library is less than v45.195
Version >NIL: "$amigaosdisk:Libs/version.library" 45 194 FILE
IF NOT $RC EQ 0 VAL
  SKIP end
ENDIF

set workbenchvalid "1"

; end
; ---

LAB end
