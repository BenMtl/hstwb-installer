.KEY file/a,dir/a
.BRA {
.KET }

; Copy Amiga OS 3.1.4 Files
; -------------------------
;
; Date: 2019-01-11
; Author: Henrik Noerfjand Stengaard
;
; AmigaDOS script to identify and copy valid Amiga OS 3.1.4 support files.


; goto end, if file doesn't exist
IF NOT EXISTS "{file}"
  SKIP end
ENDIF

; add file to log
echo "File = '{file}'" >>INSTALLDIR:Temp/Amiga-OS-314-Files.log

; goto end, if dir doesn't exist
IF NOT EXISTS "{dir}"
  SKIP end
ENDIF

; escape file
echo "{file}" >T:_escapedfile
Execute INSTALLDIR:S/Escape-Filename T:_escapedfile
set escapedfile "`type T:_escapedfile`"
Delete >NIL: T:_escapedfile

; get md5 from file
; uses tempfile set to unescaped or escaped file depending on md5 error codes
md5 "{file}" >T:_md5
IF $RC EQ 0 VAL
  set tempfile "{file}"
ELSE
  Copy >NIL: "$escapedfile" TO "INSTALLDIR:Temp/Amiga-OS-314.file"
  set tempfile "INSTALLDIR:Temp/Amiga-OS-314.file"

  md5 "$tempfile" >T:_md5
  IF NOT $RC EQ 0 VAL
    SKIP end
  ENDIF
ENDIF

; get md5 for file
set md5 ""
set md5 "`type T:_md5`"
Delete >NIL: T:_md5

; add md5 to log
echo "MD5 = '$md5'" >>INSTALLDIR:Temp/Amiga-OS-314-Files.log


; md5
; ---

; copy amiga os 3.1.4 icon pack lha, if file md5 matches
IF "$md5" eq "b1a7c9007fced12043b6d70848a3782b"
  echo "Amiga OS 3.1.4 Icon Pack lha detected using MD5" >>INSTALLDIR:Temp/Amiga-OS-314-Files.log
  set filename "amiga-os-314-iconpack.lha"
  SKIP copyfile
ENDIF

; goto end, if md5 only prefs exists
IF EXISTS INSTALLDIR:Prefs/MD5-Only
  SKIP end
ENDIF


; check filename
; --------------

; add check filename to log
echo "Check filename" >>INSTALLDIR:Temp/Amiga-OS-314-Files.log

; copy amiga os 3.1.4 icon pack lha, if file name matches
echo "{file}" NOLINE >T:_file
search >NIL: T:_file "AmigaOS-3.1.4-IconPack.lha"
IF $RC EQ 0 VAL
  echo "Amiga OS 3.1.4 Icon Pack lha detected using check filename" >>INSTALLDIR:Temp/Amiga-OS-314-Files.log
  set filename "amiga-os-314-iconpack.lha"
  SKIP copyfile
ENDIF

SKIP end


; copy file
; ---------
LAB copyfile

; add copy to log
echo "Copying to '{dir}/$filename'" >>INSTALLDIR:Temp/Amiga-OS-314-Files.log

; make file deletable, if it exists
IF EXISTS "{dir}/$filename"
  Protect >NIL: "{dir}/$filename" +prwed
ENDIF

Copy >>INSTALLDIR:Temp/Amiga-OS-314-Files.log "$escapedfile" TO "{dir}/$filename"


; end
; ---
LAB end

; delete temp files
IF EXISTS "T:_file"
  Delete >NIL: "T:_file"
ENDIF
IF EXISTS "INSTALLDIR:Temp/Amiga-OS-314.file"
  Delete >NIL: "INSTALLDIR:Temp/Amiga-OS-314.file"
ENDIF
