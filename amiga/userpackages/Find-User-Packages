.KEY userpackagesfile/a
.BRA {
.KET }

; Find User Packages
; ------------------
;
; Date: 2017-09-25
; Author: Henrik Noerfjand Stengaard

; Create empty user packages file
echo "" NOLINE >"{userpackagesfile}"

; Goto end, if USERPACKAGESDIR: device name doesn't exist
Assign >NIL: EXISTS USERPACKAGESDIR:
IF WARN
  SKIP end
ENDIF 

; List directories in USERPACKAGESDIR:
Dr "USERPACKAGESDIR:" -DO -[\f] >T:_userpackages1
sed "s/\/$//" T:_userpackages1 >T:_userpackages2
frsort <T:_userpackages2 >T:_userpackages3

; Get next user package from user packages
LAB next
set userpackage ""
set userpackage "`FirstLine T:_userpackages3 DELETE`"

; Goto end, if userpackage is empty
IF "$userpackage" EQ ""
  SKIP end
ENDIF

; Add user package to user packages file, if user package dir contains a _installdir assign file
IF EXISTS "USERPACKAGESDIR:$userpackage/_installdir"
  echo "$userpackage" >>"{userpackagesfile}"
ELSE
  echo "User package '$userpackage' doesn't contain '_installdir' assign file, skipped!"
ENDIF

SKIP BACK next


; End
LAB end

; Delete temp files, if they exist
IF EXISTS T:_userpackages
  delete >NIL: T:_userpackages#?
ENDIF